apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

version = '1.7.3'
group = 'de.timroes'

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

def sonatypeProps = 'sonatype.properties'

if(project.hasProperty(sonatypeProps) && new File(project.property(sonatypeProps)).exists()) {

    Properties props = new Properties()
    props.load(new FileInputStream(file(project.property(sonatypeProps))))
	
    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.allTasks.any { it instanceof Sign }) {
            allprojects { ext."signing.keyId" = props['signing.keyId'] }
            allprojects { ext."signing.secretKeyRingFile" = props['signing.secretKeyRingFile'] }
            allprojects { ext."signing.password" = props['signing.password'] }
        }
    }
	
    signing {
        required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    uploadArchives {

        configuration = configurations.archives
        repositories.mavenDeployer {

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: props['sonatypeRepo']) {
                authentication(userName: props['sonatypeUsername'], password: props['sonatypePassword'])
            }

            pom.project {

				name rootProject.name
                description 'Lightweight Java XML-RPC working also on Android.'
                url 'https://github.com/timroes/aXMLRPC'

                scm {
                    url 'scm:git@github.com:timroes/aXMLRPC.git'
                    connection 'scm:git@github.com:timroes/aXMLRPC.git'
                    developerConnection  'scm:git@github.com:timroes/aXMLRPC.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                    }
                }

                developers {
                    developer {
                        id 'timroes'
                        name 'Tim Roes'
                        email 'mail@timroes.de'
                    }
                }

            }

        }
    }

}